# Maintainer: Fredrik Tegenfeldt <fredrik.tegenfeldt AT unige DOT ch>
pkgname=slurm-llnl
pkgver=2.5.6
pkgrel=2
pkgdesc="Simple Linux Utility for Resource Management"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.schedmd.com"
groups=()
depends=('python2' 'libmysqlclient>=5.0.0' 'postgresql-libs' 'munge' 'lua' 'hwloc')
makedepends=('openssl>=0.9.6' 'readline' 'lua')
optdepends=('gtk2')
provides=(slurm)
conflicts=()
replaces=()
backup=()
options=()
install=${pkgname}.install
changelog=
source=("slurm-tmpfiles.conf" "slurmdbd.service" "slurmd.service" "slurmctld.service" "arch_rc.d.slurm" "arch_rc.d.slurmdbd" "shtml2html.py.patch" "http://www.schedmd.com/download/latest/slurm-${pkgver}.tar.bz2")
noextract=()

md5sums=('51e4ae2c51edf7c145d1b87bec4c344e'
         '723ae24e95a2889cb82b00bfefa42d45'
         '933688454e4feae6084ce72d63eea3c4'
         '065ec7500654b2e5e766e127505c73fe'
         'f5944208daf02cac82016d80a5619daa'
         '35e726a55591db9734110d6e09bbaadd'
         '9d8f907b340cedc28f6148fb8665f0c2'
         '8e1a4c0e71d3d79d6b7160a137bd4609')

build() {
  cd "$srcdir/slurm-$pkgver"

  patch -p0 -i ../shtml2html.py.patch

  # Fix hardcoded sbin paths
  sed -i 's./sbin/slurmstepd./bin/slurmstepd.' src/slurmd/slurmd/slurmd.c
  sed -i 's./sbin/slurmstepd./bin/slurmstepd.' src/common/env.c

  ./configure --prefix=/usr           \
              --sbindir=/usr/bin      \
              --sysconfdir=/etc/slurm \
	          --localstatedir=/       \
              --with-mysql            \
              --with-ssl              \
              --with-munge            \
              --with-readline
  make
}

package() {
  cd "$srcdir/slurm-$pkgver"

  make DESTDIR="$pkgdir/" install

  # slurm configs and license
  install -D -m644 etc/slurm.conf.example    "${pkgdir}/etc/slurm/slurm.conf.example"
  install -D -m644 etc/slurmdbd.conf.example "${pkgdir}/etc/slurm/slurmdbd.conf.example"
  install -D -m644 LICENSE.OpenSSL           "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.OpenSSL"
  install -D -m644 COPYING                   "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # init related install
  install -D -m755 ../arch_rc.d.slurm    "${pkgdir}/etc/rc.d/slurm"
  install -D -m755 ../arch_rc.d.slurmdbd "${pkgdir}/etc/rc.d/slurmdbd"

  # SystemD related install
  install -D -m644 ../slurmd.service        "${pkgdir}/usr/lib/systemd/system/slurmd.service"
  install -D -m644 ../slurmctld.service     "${pkgdir}/usr/lib/systemd/system/slurmctld.service"
  install -D -m644 ../slurmdbd.service      "${pkgdir}/usr/lib/systemd/system/slurmdbd.service"
  install -D -m644 ../slurm-tmpfiles.conf   "${pkgdir}/etc/tmpfiles.d/slurm-tmpfiles.conf"
}
