# Maintainer: Nicolas Qui√©not < niQo at aur >
# Contributor: Alan Orth <alan.orth@gmail.com>

pkgname=dmapd
pkgver=0.0.51
pkgrel=4
pkgdesc="Serve DAAP (iTunes) and DPAP (iPhoto) shares from Linux"
arch=('i686' 'x86_64')
url="http://www.flyn.org/projects/dmapd/"
license=('GPL')
depends=('libdmapsharing' 'graphicsmagick' 'libexif')
makedepends=()
options=('!libtool')
backup=('etc/dmapd.conf')
source=("http://www.flyn.org/projects/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('6f6de9b3ca7cd9955902b5368f553e52')

install=dmapd.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # fix username in default config
  sed -i 's/User=dmapd/User=daemon/' distro/dmapd.conf

  # fix daemon path in service file
  sed -i 's/sbin/bin/' distro/dmapd.service

  # create cache directories
  install -d ${pkgdir}/var/cache/dmapd || return 1
  install -d ${pkgdir}/var/cache/dmapd/DAAP || return 1
  install -d ${pkgdir}/var/cache/dmapd/DPAP || return 1

  # copy distribution files
  install -D -m 0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
  install -D -m 0644 distro/dmapd.conf ${pkgdir}/etc/dmapd.conf || return 1
  install -D -m 0755 distro/dmapd.service ${pkgdir}/usr/lib/systemd/system/dmapd.service || return 1
}
